apply plugin: 'java'
apply plugin: 'edu.sc.seis.macAppBundle'
apply plugin: 'nebula.ospackage'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
compileJava.options.incremental = true

sourceSets.main.java.srcDirs = [ "src/" ]

project.ext.mainClassName = "com.crashinvaders.texturepackergui.desktop.ApplicationStarter"
project.ext.distFileName = "$project.appName-$project.ext.version"
project.ext.resourceDirs = files('resources/', "$rootDir/assets/")

project.ext.lwjglVersion = "3.3.0-SNAPSHOT"
project.ext.lwjglMacosNatives = "natives-macos-arm64"

def nativePlatforms = [
        "natives-windows",
        "natives-windows-x86",
        "natives-linux",
        "natives-linux-arm32",
        "natives-linux-arm64",
        "natives-macos",
        "natives-macos-arm64"
]

dependencies {
    runtimeOnly files(project.resourceDirs)
    implementation project(":core")
    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    implementation "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
    implementation "com.crashinvaders.lml:gdx-autumn-fcs:$lmlVersion"
    implementation "args4j:args4j:$args4jVersion"

    // This is a temporary LWJGL 3.3 snapshot version override,
    // as current LibGDX (1.10.0) is stuck with LWJGL 3.2 which doesn't yet support M1 macOS.
    //TODO Update/remove when stable LWJGL 3.3 is out or LibGDX official backend adds support for ARM macOS.
    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")
    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-jemalloc"
    implementation "org.lwjgl:lwjgl-openal"
    implementation "org.lwjgl:lwjgl-opengl"
    nativePlatforms.each {
        runtimeOnly "org.lwjgl:lwjgl::$it"
        runtimeOnly "org.lwjgl:lwjgl-glfw::$it"
        runtimeOnly "org.lwjgl:lwjgl-jemalloc::$it"
        runtimeOnly "org.lwjgl:lwjgl-openal::$it"
        runtimeOnly "org.lwjgl:lwjgl-opengl::$it"
    }
}

task run(dependsOn: classes, type: JavaExec) {
    main = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    classpath(project.resourceDirs)
    standardInput = System.in
    ignoreExitValue = true

    if (org.gradle.internal.os.OperatingSystem.current().isMacOsX()) {
        jvmArgs += ['-XstartOnFirstThread', '-Djava.awt.headless=true']
    }
}

task dist(type: Jar) {
    dependsOn configurations.runtimeClasspath
    from configurations.runtimeClasspath.collect {it.isDirectory() ? it : zipTree(it) } with jar

    // Exclude unnecessary JARs that bundled with Tinify Maven artifact.
    // Remove after https://github.com/tinify/tinify-java/issues/9 gets resolved.
    exclude("/gson-*.jar",
            "/okhttp-*.jar",
            "/okio-*.jar")

    manifest {
        attributes 'Main-Class': project.mainClassName
    }
}

// Create fat and OS based (stripped natives) distribution release tasks with names "distRelease[Platform]".
void generateDistReleaseTasks() {
    def osFamilyList = [
            OperatingSystemFamily.LINUX,
            OperatingSystemFamily.MACOS,
            OperatingSystemFamily.WINDOWS,
            null]
    osFamilyList.each {String osFamily ->
        String taskName = "distRelease" + (osFamily != null ? toCamelCase(osFamily, true) : "")
//        println "Creating task \"$taskName\""
        tasks.create(name: taskName, type: Jar) {
            group 'Distribution'
            description 'Generates a destributable JAR file' + (osFamily != null ? " for ${toCamelCase(osFamily, true)}." : ".")

            dependsOn configurations.runtimeClasspath
            from configurations.runtimeClasspath.collect {it.isDirectory() ? it : zipTree(it) } with jar

            // Exclude unnecessary JARs that bundled with Tinify Maven artifact.
            // Remove after https://github.com/tinify/tinify-java/issues/9 gets resolved.
            exclude("/gson-*.jar",
                    "/okhttp-*.jar",
                    "/okio-*.jar")

            manifest {
                attributes 'Main-Class': project.mainClassName
            }

            def osFamilySuffix = osFamily != null ? "-${osFamily.toLowerCase()}" : ""
            archiveFileName = "$project.appName$osFamilySuffix.$extension"
            destinationDirectory = file("$rootDir/distribution/output")

            // Strip the natives unused by the current platform.
            if (osFamily != null) {
                if (OperatingSystemFamily.LINUX != osFamily)
                    exclude("**/*.so",
                            "**/*.so.*",
                            "/etctool/etctool-linux")
                if (OperatingSystemFamily.WINDOWS != osFamily)
                    exclude("**/*.dll",
                            "**/*.dll.*",
                            "/etctool/etctool.exe")
                if (OperatingSystemFamily.MACOS != osFamily)
                    exclude("**/*.dylib",
                            "**/*.dylib.*",
                            "/etctool/etctool-mac")
            }
        }
    }
}
generateDistReleaseTasks()

task zipRelease(type: Zip) {
    from distRelease
    from file("$rootDir/distribution/files")
    archiveFileName = "$project.distFileName.$extension"
    destinationDirectory = file("$rootDir/distribution/output")
}
zipRelease.dependsOn distRelease

//region Windows distribution (NSIS Installer).
/**
 * Task to create Windows installer using NSIS.
 *
 * In order to make it work, you need:
 *     1. Windows OS only.
 *     2. NSIS 3 installed on your system and added to PATH (makensis.exe).
 *          https://sourceforge.net/projects/nsis/files/
 *     3. FileAssoc macros should be installed.
 *          Copy code from http://nsis.sourceforge.net/FileAssoc
 *          into file "FileAssoc.nsh" and put it under "<NSIS_DIR>/Include/"
 *
 * After task execution, the output files can be found under "./distribution/output/".
 */
task nsisInstaller(type: Exec) {
    group 'NSIS Installer'
    description 'Assembles a Windows NSIS installer distribution.'

    // NSIS installer can be created only on Windows.
    if (!org.gradle.internal.os.OperatingSystem.current().isWindows()) {
        return
    }

    workingDir '../distribution'

    def nsisCommand = "makensis /DFILENAME=\"$project.distFileName\" installer-nsis.nsi"
    commandLine 'cmd', '/c', "$nsisCommand"
}
nsisInstaller.dependsOn distReleaseWindows
//endregion

//region MacOS distribution (Mac App Bundle).
macAppBundle {
    mainClassName = project.mainClassName
    bundleIdentifier = 'com.crashinvaders.texturepackergui'
    appStyle = 'universalJavaApplicationStub'
    jvmVersion = '1.8+'
    javaProperties.put("apple.laf.useScreenMenuBar", "true")
    javaExtras.put('-XstartOnFirstThread', null)
    javaExtras.put('-Djava.awt.headless', "true")
    bundleExtras.put('CFBundleVersion', project.ext.version)
    bundleExtras.put('CFBundleShortVersionString', project.ext.version)
    // This one comes as a recommendation from
    // https://github.com/tofi86/universalJavaApplicationStub#recommended-additional-plist-keys
    bundleExtras.put('NSAppleEventsUsageDescription', 'There was an error while launching the application. Please click OK to display a dialog with more information or cancel and view the syslog for details.')
    bundleJRE = false
    appName = "GDX Texture Packer"
    dmgName = "${project.appName}-${project.ext.version}"
    icon = "icons.icns"
//    backgroundImage = "doc/macbackground.png"
}

// Since macAppBundle provide very little control over how to bundle the jar,
// we have to swap files by hand.
task cleanMacAppJavaDir (type: Delete) {
    delete fileTree(dir: "$project.buildDir/$macAppBundle.appOutputDir/${macAppBundle.appName}.app/Contents/Java")
}
task copyMacAppFiles (type: Copy) {
    from "$rootDir/distribution/macApp"
    into "$project.buildDir/$macAppBundle.appOutputDir/${macAppBundle.appName}.app/Contents"
}
task copyMacAppDistJar(type: Copy) {
    from "$rootDir/distribution/output/${project.appName}-macos.jar"
    into "$project.buildDir/$macAppBundle.appOutputDir/${macAppBundle.appName}.app/Contents/Java/"
    // Mimic the "jar" task's output artifact name as the run script expects it.
    rename { "${project.name}.jar" }
}
task copyDmgToDistDir(type: Copy) {
    // Copy the resulting DMG file to the global dist output location.
    from "${project.buildDir}/${project.distsDirName}/${macAppBundle.dmgName}.dmg"
    into "$rootDir/distribution/output"
}

createDmg.finalizedBy copyDmgToDistDir
createApp.finalizedBy copyMacAppDistJar, copyMacAppFiles
copyMacAppDistJar.dependsOn distReleaseMacos, cleanMacAppJavaDir
cleanMacAppJavaDir.dependsOn generatePlist

//endregion

//region Linux distribution (ospackage).
// https://github.com/nebula-plugins/gradle-ospackage-plugin/wiki
ospackage {
    release = 1
    version = project.ext.version
    packageName = project.appName
    os = 'LINUX' // only applied to RPM

    vendor 'Crashinvaders'
    url 'https://github.com/crashinvaders/gdx-texture-packer-gui'
    maintainer 'Anton Chekulaev <anton@crashinvaders.com>'
    summary 'LibGDX texture atlas packer GUI'
    packageDescription 'A simple utility to help you pack and manage texture atlases for LibGDX game framework. It\'s mostly just a visual wrapper over LibGDX TexturePacker classes and provides a convenient way to use it.'
    license file("$rootDir/distribution/license.txt").text

    // JRE 8 is the minimum supported runtime.
    requires "java8-runtime"

    postInstall '#!/usr/bin/env bash\n' +
            'sudo update-mime-database /usr/share/mime\n' +
            'sudo update-icon-caches /usr/share/icons/hicolor'

    def installationDir = '/opt/gdx-texture-packer'
    into installationDir

    from("$rootDir/distribution/output/${project.appName}-linux.jar") {
        rename { "${project.appName}.jar" }
    }
    from("$rootDir/distribution/files/launcher_linux.sh") { fileMode 0555 }
    from("$rootDir/distribution/files/readme.txt")
    from("$rootDir/distribution/files/readme_hotkeys.txt")
    from("$rootDir/distribution/license.txt")

    from("$rootDir/resources/icon256.png") {
        into '/usr/share/icons/hicolor/256x256/mimetypes'
        rename { 'application-gdx-tp-project.png' }
    }
    from("$rootDir/resources/icon256.png") {
        into '/usr/share/icons/hicolor/256x256/apps'
        rename { 'gdx-texture-packer.png' }
    }

    from(new File("$project.buildDir/tmp", 'gdx-texture-packer.desktop').with {
        it.parentFile.mkdirs()
        it.write """[Desktop Entry]
                |Name=GDX Texture Packer
                |GenericName=Texture Packer
                |Comment=$summary
                |Version=$version
                |Keywords=texture;atlas;gdx;libgdx;packer;
                |Type=Application
                |Categories=Graphics;Development;
                |MimeType=application/gdx-tp-project;
                |Terminal=false
                |Exec=$installationDir/launcher_linux.sh %U
                |Icon=gdx-texture-packer""".stripMargin()
        return it.absolutePath
    }) {
        into '/usr/share/applications/'
    }

    from("$rootDir/distribution/linux-packages/project-mime-type.xml") {
        rename { 'gdx-texture-packer.xml' }
        into '/usr/share/mime/packages/'
    }
}

buildRpm {
    group 'OSPackage'
    description 'Assembles an RPM Linux distribution package.'
    dependsOn distReleaseLinux
}

buildDeb {
    group 'OSPackage'
    description 'Assembles a DEB Linux distribution package.'
    dependsOn distReleaseLinux
}
//endregion

//region Utils
static String toCamelCase(String text, boolean capitalized = false) {
    text = text.replaceAll("(_)([A-Za-z0-9])", { String[] it -> it[2].toUpperCase() })
    return capitalized ? text.capitalize() : text
}

static String toSnakeCase(String text) {
    text.replaceAll(/([A-Z])/, /_$1/).toLowerCase().replaceAll(/^_/, '')
}
//endregion